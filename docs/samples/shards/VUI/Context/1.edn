(Setup
 (GFX.DrawQueue) >= .queue

  ; Create render steps
 (GFX.BuiltinFeature :Id BuiltinFeatureId.Transform) >> .features
 {:Features .features :Queue .queue :Sort SortMode.Queue} (GFX.DrawablePass) >> .render-steps

  ; Panel transforms
 15.0 (Math.DegreesToRadians) (Math.AxisAngleY) (Math.Rotation) >= .tmp
 (Float3 -1.0 0.0 0.0) (Math.Translation) (Math.MatMul .tmp) >= .panel-t-0
 -15.0 (Math.DegreesToRadians) (Math.AxisAngleY) (Math.Rotation) >= .tmp
 (Float3 1.0 0.0 0.0) (Math.Translation) (Math.MatMul .tmp) >= .panel-t-1
 5.0 (Math.DegreesToRadians) (Math.AxisAngleX) (Math.Rotation) >= .tmp
 (Float3 0.0 1.2 0.0) (Math.Translation) (Math.MatMul .tmp) >= .panel-t-2

  ; Camera
 {:Position (Float3 1 2 8) :Target (Float3 0 0 0)} (Math.LookAt) >= .view-transform
 (GFX.View :View .view-transform) >= .view)

(GFX.MainWindow
 :Title "World space UI" :Width 1280 :Height 720
 :Contents
 (->
  .queue (GFX.ClearQueue)
  (VUI.Context
   :Queue .queue :View .view :Scale 100.0 :Contents
   (->
    (VUI.Panel
     :Transform .panel-t-0 :Size (Float2 100 100) :Contents
     (->
      (UI.CentralPanel
       (->
        "Left panel" (UI.Label)
        (UI.Button :Label "Button")))))
    (VUI.Panel
     :Transform .panel-t-1 :Size (Float2 100 100) :Contents
     (->
      (UI.CentralPanel
       (->
        "Right panel" (UI.Label)
        (UI.Button :Label "Button")))))
    (VUI.Panel
     :Transform .panel-t-2 :Size (Float2 300 60) :Contents
     (->
      (UI.CentralPanel
       (->
        "Wide panel" (UI.Label)
        (Setup
         (LoadImage "../../assets/ShardsLogo.png") (GFX.Texture) >= .button-texture)
        (UI.Horizontal (->
                        .button-texture (UI.ImageButton :Scale (Float2 0.01))
                        .button-texture (UI.ImageButton :Scale (Float2 0.01))
                        .button-texture (UI.ImageButton :Scale (Float2 0.01))))))))))

  (GFX.Render :Steps .render-steps :View .view)))
