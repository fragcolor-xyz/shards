(def cube-layout ["position", "color"])
(def cube
  {"Vertices"
   [(Float3 -1.0  1.0  1.0) (Color 0x00 0x00 0x00 0xff)
    (Float3  1.0  1.0  1.0) (Color 0x00 0x00 0xff 0xff)
    (Float3 -1.0 -1.0  1.0) (Color 0x00 0xff 0x00 0xff)
    (Float3  1.0 -1.0  1.0) (Color 0x00 0xff 0xff 0xff)
    (Float3 -1.0  1.0 -1.0) (Color 0xff 0x00 0x00 0xff)
    (Float3  1.0  1.0 -1.0) (Color 0xff 0x00 0xff 0xff)
    (Float3 -1.0 -1.0 -1.0) (Color 0xff 0xff 0x00 0xff)
    (Float3  1.0 -1.0 -1.0) (Color 0xff 0xff 0xff 0xff)]
   "Indices"
   [0 1 2
    1 3 2
    4 6 5
    5 6 7
    0 2 4
    4 2 6
    1 5 3
    5 7 3
    0 4 1
    4 5 1
    2 3 6
    6 3 7]})

(def timestep (/ 1.0 120.0))
(def Root (Node))
(def test-chain
  (Chain
   "test-chain"
   :Looped
   (GFX.MainWindow
    :Title "SDL Window" :Width 1280 :Height 720 :Debug true
    :Contents
    (-> (Setup
         cube (GFX.Mesh :Layout cube-layout) >= .mesh
         (Float3 0 0 0) (Math.Translation) >= .transform
         0.0 >= .time
         (GFX.Drawable :Mesh .mesh :Transform .transform) >= .drawable)

        .time (Math.Add timestep) >= .time
        (Log "Time")
        .time (Math.Multiply 0.2) (Math.AxisAngleX) (Math.Rotation) >= .rotX
        .time (Math.Multiply 0.7) (Math.AxisAngleY) (Math.Rotation) >= .rotY
        .time (Math.Multiply 0.9) (Math.AxisAngleZ) (Math.Rotation) >= .rotZ
		.rotX (Math.MatMul .rotY) (Math.MatMul .rotZ) >= .transform
		(Log "Transform")
        [.drawable] (GFX.Draw)))))

(schedule Root test-chain)
(run Root timestep)