@define(timestep #(1.0 | Math.Divide(120.0)))
@mesh(root)
@define(blendAdd {Operation: BlendOperation::Add Src: BlendFactor::One Dst: BlendFactor::One})
@define(blendAlpha {Operation: BlendOperation::Add Src: BlendFactor::SrcAlpha Dst: BlendFactor::OneMinusSrcAlpha})
@define(blendOne {Operation: BlendOperation::Add Src: BlendFactor::One Dst: BlendFactor::One})

@template(spin-transform [t location] {
  t
  {Math.Multiply(0.2) | Math.AxisAngleX | Math.Rotation >= rotX}
  {Math.Multiply(0.7) | Math.AxisAngleY | Math.Rotation >= rotY}
  {Math.Multiply(0.9) | Math.AxisAngleZ | Math.Rotation >= rotZ}
  location | Math.Translation | Math.MatMul(rotX) | Math.MatMul(rotY) | Math.MatMul(rotZ)
})

@wire(test-wire {
  Once({
    GFX.BuiltinMesh(Type: BuiltinMeshType::Cube) >= mesh
    @f3(-1 0 0) | Math.Translation >= transform-0
    @f3(1 0 0) | Math.Translation >= transform-1
    @f3(0 0 -1) | Math.Normalize >= light-direction
    
    "vec3<f32>(0.0, 1.0, 0.2)" = fconst-str
  })
  
  Animation.Timer = time
  
  GFX.BuiltinFeature(BuiltinFeatureId::Transform) >> features
  ; Create render steps
  GFX.Feature(
    Shaders: [{Name: "lighting"
               Stage: ProgrammableGraphicsStage::Fragment
               EntryPoint: {
          Shader.ReadInput("worldNormal") >= normal
          Shader.ReadBuffer("lightDirection") >= light-dir
          @f3(0 0 0) | Math.Subtract(light-dir) >= inv-light-dir
          normal | Math.Normalize | Math.Dot(inv-light-dir) | Max(0.0) >= n-dot-l
          Shader.ReadBuffer("lightColor") | Math.Multiply(n-dot-l)
          {@f3(0.0) >= extra-color
            Shader.Literal([extra-color " = " fconst-str ";\n"])
          }
          Math.Add(extra-color)
          Shader.WriteGlobal("lighting")
        }}
      {Name: "mainColor"
       Stage: ProgrammableGraphicsStage::Fragment
       EntryPoint: {
          Shader.ReadBuffer("time") >= time
          time | Math.Multiply(2.0) | Math.Cos | Math.Divide(2.0) | Math.Add(0.5) >= wave
          Shader.ReadGlobal("lighting") | ToFloat4 >= lighting-4
          wave | Math.Multiply(@f4(0 0 0 1)) | Math.Add(lighting-4)
          Shader.WriteOutput("color")
        }}]
    ComposeWith: {fconst-str: fconst-str}
    State: {DepthCompare: CompareFunction::Less
            Blend: {Color: @blendAlpha Alpha: @blendOne}}
    ViewGenerators: {
      {time: time}
    }
    Params: {lightDirection: light-direction
             lightColor: @f3(1.0)}
  ) >> features
  GFX.DrawQueue >= queue
  GFX.DrawablePass(Features: features Queue: queue) >> render-steps
  
  ; Create view
  {Position: @f3(0 0 5) Target: @f3(0 0 0)} | Math.LookAt >= view-transform
  GFX.View(View: view-transform) >= view
  GFX.MainWindow(
    Title: "Shards Window" Width: 1280 Height: 720
    Contents: {
      @spin-transform(time @f3(-0.6 0 0))
      GFX.Drawable(Mesh: mesh) | GFX.Draw(queue)
      
      @spin-transform((time | Math.Multiply(0.5)) @f3(0.6 0.0 0))
      GFX.Drawable(Mesh: mesh) | GFX.Draw(queue)
      
      GFX.Render(View: view Steps: render-steps)
    }
  )
} Looped: true)

@schedule(root test-wire)
@run(root FPS: 120 Iterations: 100) | Assert.Is(true)
