; NOTE: This shader is converted from https://www.shadertoy.com/view/tlVGDt
; With slight modifications

@define(timestep #(1.0 | Math.Divide(120.0)))

; Special modulo since % is negative for negatives
@template(mod-positive [div] {
  {Math.Divide(div) | Math.Floor | Math.Multiply(div) >= mod-wholes} |
  Math.Subtract(mod-wholes)
})

@wire(rot {
  Set(input)
  input | Math.Cos >= c
  input | Math.Sin |
  {Set(s)} |
  {Math.Negate >= neg-s} |
  @f2(c s) >> result
  @f2(neg-s c) >> result
  result
} Pure: true)

@wire(box-sdf-base {
  box-pos | Math.Abs | Math.Subtract(box-base) >= q
  q | {Take(0) >= q-x} | {Take(1) >= q-y} | {Take(2) >= q-z}
  q-x | Max(q-y) | Max(q-z) | Min(0.0) >= tmp
  q | Max(@f3(0.0)) | Math.Length | Math.Add(tmp)
})

@wire(box-sdf {
  pos | Math.Multiply(scale) >= box-pos
  @f3(0.4 0.4 0.1) >= box-base
  Do(box-sdf-base) | Math.Divide(1.5) | Math.Negate
})

@template(box-elem [ops] {
  pos-orig > pos
  pos | {Take(0) > x} | {Take(1) > y} | {Take(2) > z}
  gtime | Math.Multiply(0.4) | Math.Sin | Math.Multiply(2.5) | ops
  @f2(x y) > tmp-2
  0.8 | Do(rot) | Math.MatMul(tmp-2) |
  {Take(0) > x} | {Take(1) > y} | @f3(x y z) > pos

  gtime | Math.Multiply(0.4) | Math.Sin | Math.Abs | Math.Multiply(1.5) > scale
  2.0 | Math.Subtract(scale) > scale

  Do(box-sdf)
})

@wire(distance-field {
  pos = pos-orig
  0.0 >= x 0.0 >= y 0.0 >= z
  @f2(0.0) >= tmp-2
  0.0 >= scale

  @box-elem({Math.Add(y) > y}) >= box-1
  @box-elem({Math.Subtract(y) > y}) >= box-2
  @box-elem({Math.Add(x) > x}) >= box-3
  @box-elem({Math.Subtract(x) > x}) >= box-4

  box-1 | Max(box-2) | Max(box-3) | Max(box-4)
})

@define(shader-main {
  Shader.ReadBuffer(Name: "viewport" BufferName: "view") >= viewport
  Shader.ReadInput("texCoord0") >= uv
  Shader.ReadBuffer(Name: "time" BufferName: "view") >= time

  ; Convert uv to screen position & aspect correct normalize
  viewport | Take([2 3]) >= screen-size |
  Math.Multiply(uv) >= screen-pos
  screen-size | Take(0) >= screen-w
  screen-size | Take(1) >= screen-h
  screen-w | Min(screen-h) >= screen-min-dim
  screen-pos | Math.Multiply(2.0) | Math.Subtract(screen-size) | Math.Divide(screen-min-dim) | Math.Multiply(@f2(1.0 -1.0)) >= uv1

  ; Compute ray
  time | Math.Multiply(4.0) >= z
  @f3(0.0 -0.2 z) >= ro
  uv1 | {Take(0) >= x} | {Take(1) >= y}
  @f3(x y 1.5) | Math.Normalize >= ray

  ray | {Take(0) > x} | {Take(1) > y} | {Take(2) > z}

  ; Rotate ray
  @f2(x y) >= tmp-2
  time | Math.Multiply(0.03) | Math.Sin | Math.Multiply(5.0) | Do(rot) | Math.MatMul(tmp-2) >= tmp-2
  tmp-2 | Take(0) > x
  tmp-2 | Take(1) > y
  @f2(y z) > tmp-2
  time | Math.Multiply(0.05) | Math.Sin | Math.Multiply(0.2) | Do(rot) | Math.MatMul(tmp-2) >= tmp-2
  tmp-2 | Take(0) > y
  tmp-2 | Take(1) > z

  @f3(x y z) > ray

  0.1 >= t
  @f3(0.0) >= col
  0.0 >= ac
  0.0 >= d

  ForRange(0 99 {
    Ref(i)
    ray | Math.Multiply(t) | Math.Add(ro) >= pos |
    Math.Subtract(2.0) | @mod-positive(4.0) | Math.Subtract(2.0) > pos

    i | ToFloat | Math.Multiply(0.01) >= tmp
    time | Math.Subtract(tmp) | Set(gtime Global: true)

    Do(distance-field) |
    Math.Abs | Max(0.01) > d

    d | Math.Negate | Math.Multiply(23.0) | Math.Exp |
    Math.Add(ac) > ac

    d | Math.Multiply(0.55) | Math.Add(t) > t
  })

  @f3(ac) | Math.Multiply(0.02) > col
  time | Math.Sin | Math.Abs | Math.Multiply(0.2) > y
  time | Math.Sin | Math.Multiply(0.2) | Math.Add(0.5) > z
  @f3(0.0 y z) | Math.Add(col) > col

  time | Math.Sin | Math.Multiply(0.02) | Math.Add(0.02) >= a
  1.0 | Math.Subtract(a) > a

  col | {Take(0) > x} | {Take(1) > y} | {Take(2) > z}
  @f4(x y z 1.0) | Shader.WriteOutput("color")
  ; End shader
})

@wire(gfx {
  Animation.Timer = time

  GFX.Feature(ViewGenerators: {
    {time: time}
  }) >> features
  GFX.EffectPass(EntryPoint: {@shader-main} Features: features) >> render-steps

  GFX.Render(Steps: render-steps)
} Looped: true)

@wire(main-loop-wire {
  GFX.MainWindow(Title: "Shards Window" Width: 1280 Height: 720
                 Contents: {Step(gfx)})
} Looped: true LStack: 4194304)

@mesh(root)
@schedule(root main-loop-wire)
@run(root FPS: 120 Iterations: 100) | Assert.Is(true)